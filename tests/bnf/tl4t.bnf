/**
    Toy Language 4 Typing
**/

source =
[   
    @ignore("C/C++")
    [
        [#info:i stmt:s #new_rootstmt(_, s, i)]+
        eof
    ]
]

stmt =
[
    [decl_var | decl_fun | expr]:>_
]

decl_var =
[
    __scope__:t
    __scope__:e
    #info:i
    "var" name:n [ ':' type:t ]? [';' | '=' expr:>e ]? #new_declvar(_, n, t, e, i)
]

decl_fun =
[
    #info:i
    "fun" name:n params:p ':' type:t implem:b #new_declfun(_, n, t, p, b, i)
]

params =
[
    '(' [
            param:p #add_param(_, p)
            [',' param:p #add_param(_, p) ]*
            [',' '...' #add_param_variadic(_) ]?
        |   '...' #add_param_variadic(_)
        ]?
    ')'
]

param =
[
    #info:i
    name:n ':' type:t #new_param(_, n, t, i)
]

implem =
[
    ';' /* just a prototype */
    | block:>_
]

name = [ id ]

type = [ id ]

block =
[
    '{' [ #info:i stmt:s #new_stmt(_, s, i) ]+ '}'
]

expr = [ #info:i assignement_expression:e ';' #new_expr_stmt(_, e, i) ]

assign_op = [
    @ignore('null')
    [ #info:i
        [
            '=' !'='
            | "+="
            | "-="
            | "*="
            | "/="
            | "%="
            | "<<="
            | ">>="
            | "&="
            | "^="
            | "|="
        ]:op
        #new_operator(_, op, i)
    ]
]
assignement_expression =
[
    #info:i
    or_expression:>_
    [
        assign_op:op
        assignement_expression:param
        #new_binary(_, op, param, i)
    ]*
]

or_op = [ @ignore('null') #info:i [["|" !["|"|"="]]:op #new_operator(_, op, i)] ]
or_expression =
[
    #info:i
    xor_expression:>_
    [   
        or_op:op
        xor_expression:param
        #new_binary(_, op, param, i)
    ]*
]

xor_op = [ @ignore('null') #info:i [ ["^" !"="]:op #new_operator(_, op, i) ] ]
xor_expression =
[
    #info:i
    and_expression:>_
    [   
        xor_op:op
        and_expression:param
        #new_binary(_, op, param, i)
    ]*
]

and_op = [ @ignore('null') #info:i [ ["&" !["&"|"="]]:op #new_operator(_, op, i) ] ]
and_expression =
[
    #info:i
    equality_expression:>_
    [   
        and_op:op
        equality_expression:param
        #new_binary(_, op, param, i)
    ]*
]

eqneq_op = [ @ignore('null') #info:i [ ["==" | "!="]:op #new_operator(_, op, i) ] ]
equality_expression =
[
    #info:i
    relational_expression:>_
    [   
        eqneq_op:op
        relational_expression:param
        #new_binary(_, op, param, i)
    ]*
]

cmp_op = [
    @ignore('null')
    #info:i
    [
        "<="
        | ">="
        | '<' !'<'
        | '>' !'>'
    ]:op
    #new_operator(_, op, i)
]
relational_expression =
[
    #info:i
    shift_expression:>_
    [   
        cmp_op:op
        shift_expression:param
        #new_binary(_, op, param, i)
    ]*
]

shift_op = [
    @ignore('null') #info:i ["<<" !"=" | ">>" !"="]:op 
    #new_operator(_, op, i) 
]
shift_expression =
[
    #info:i
    additive_expression:>_
    [   
        shift_op:op
        additive_expression:param
        #new_binary(_, op, param, i)
    ]*
]

add_op = [
    @ignore('null')
    #info:i
    [
        '+' !['+'|'=']
        | '-' !['-'|'='|'>']
    ]:op
    #new_operator(_, op, i)
]
additive_expression =
[
    #info:i
    multiplicative_expression:>_
    [   
        add_op:op
        multiplicative_expression:param
        #new_binary(_, op, param, i)
    ]*
]

mul_op = [ @ignore('null') #info:i [['*'|'/'|'%']:op !'='] #new_operator(_, op, i) ]
multiplicative_expression =
[
    #info:i
    unary_expression:>_
    [   
        mul_op:op
        unary_expression:param
        #new_binary(_, op, param, i)
    ]*
]

unary_op =
[
    @ignore('null') 
    #info:i
    [
     '~' !'='
    | '!' !'='
    | '+' !'='
    | '-' !['>'|'=']
    ]:op #new_operator(_, op, i)
]
unary_expression =
[
    postfix_expression:>_
    |
        __scope__:op
        #info:i
        [   unary_op:>op
        |   #info:i
            Base.id:ident
            #new_operator(op, ident, i)
        ]
        unary_expression:expr
        #new_unary(_, op, expr, i)
]

postfix_expression =
[
    primary_expression:>_
    [
        __scope__:pexp
        #info:i
        [
/*        '[' expression:expr ']' #new_array_call(e, e, expr) */
          '(' func_arg_list?:args ')' #new_func_call(pexp, _, args, i)
/*
        | '.' identifier:ident #new_dot(_, _, ident, i)
        | "->" identifier:ident #new_arrow(_, _, ident, i)
        | ["++"|"--"]:op #new_operator(op, op, i) #new_post(_, op, _, i)
*/
        ]
        #bind('_', pexp)
    ]*
]

func_arg_list =
[
    assignement_expression:a #new_arg(_, a)
    [
        ','
        assignement_expression:a #new_arg(_, a)
    ]*
]

primary_expression =
[
    #info:i '(' or_expression:expr ')' #new_paren(_, expr, i)
    |[ literal | identifier]:>_
]

identifier =
[
    @ignore('null') 
    [
        #info:i
        rootidentifier:id
/*        #check_is_id(id) */
        #new_id(_, id, i)
    ]
]

rootidentifier = [ Base.id:>_ ]

dot = [ '.' !'.' ]

pow = [ 'p' | 'P' ]

exp = [ ['e'|'E'] ['+'|'-']? ['0'..'9']+ ]

unsigned_suffix = [ 'u' | 'U' ]

long_suffix = [ 'l' | 'L' ]

float_suffix = [ 'f' | 'F' ]

complex_suffix = [ 'i' | 'I' | 'j' | 'J' ]

decimal_const =
[
    ['0'..'9']+
    unsigned_suffix?
    long_suffix?
    long_suffix?
]

hexadecimal_prefix = [ '0' ['x'|'X'] ]

hexadecimal_digit = [ '0'..'9' | 'a'..'f' | 'A'..'F' ]

hexadecimal_const_int =
[
    hexadecimal_prefix [hexadecimal_digit]+
    unsigned_suffix?
    long_suffix?
    long_suffix?
]

octal_digit = [ '0'..'7' ]

octal_const = 
[
    '0' octal_digit+
    [
       dot octal_digit+
       [pow ['+'|'-']? decimal_const]?
    ]?
]

double_const =
[
    [decimal_const dot ['0'..'9']*| dot ['0'..'9']+] exp?
    long_suffix?
    float_suffix?
    complex_suffix?
]

encoding_prefix = [ "u8" | 'u' | 'U' | 'L' ]

string_const = [ encoding_prefix? Base.string @ignore("C/C++") Base.string* ]

char_const = [ encoding_prefix? Base.char ]

literal =
[
    @ignore('null')
    [
    __scope__:t
    #info:i
    [
        hexadecimal_const_int #set(t, "int")
        | octal_const #set(t, "int")
        | double_const #set(t, "double")
        | decimal_const #set(t, "int")
        | string_const #set(t, "string")
        | char_const #set(t, "char")
    ]:val
    #new_literal(_, val, t, i)
    ]
]
